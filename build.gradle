group 'io.lumigo.javaagent'

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    maven {
      name = "sonatype"
      url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
  }
  dependencies {
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.19.0"
    classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    classpath "io.opentelemetry.instrumentation:gradle-plugins:1.27.0-alpha"
  }
}

subprojects {
  version = rootProject.version

  apply plugin: "java"
  // Exclude OTeL instrumentation override sub projects
  if (it.name != "apache-httpclient-5.0" && it.name != "java-http-client" && it.name != "library") {
    apply plugin: "com.diffplug.spotless"

    spotless {
      java {
        googleJavaFormat()
        licenseHeaderFile(rootProject.file("./gradle/spotless.license.java"), "(package|import|public)")
        target("src/**/*.java")
      }
    }
  }

  tasks.withType(Test) {
    testLogging {
      exceptionFormat "full"
      events "started", "skipped", "passed", "failed"
      showStandardStreams true
    }
  }

  ext {
    versions = [
      opentelemetrySdk           : "1.27.0",
      opentelemetryContrib       : "1.27.0-alpha",
      opentelemetryJavaagent     : "1.27.0",
      opentelemetryJavaagentAlpha: "1.27.0-alpha",

      // Instrumentation versions
      apachehttpclient50         : "5.0",
      awssdk22                   : "2.2.0",

      jackson                    : "2.15.2",
      bytebuddy                  : "1.14.5",
      autoservice                : "1.1.1",
      autovalue                  : "1.10.1",
      junit                      : "5.9.3",
      mockito                    : "5.4.0",
      hamcrest                   : "2.2"
    ]

    deps = [
      bytebuddy  : "net.bytebuddy:byte-buddy-dep:${versions.bytebuddy}",
      autoservice: [
        "com.google.auto.service:auto-service:${versions.autoservice}",
        "com.google.auto.service:auto-service-annotations:${versions.autoservice}",
      ]
    ]
  }

  repositories {
    mavenCentral()
    maven {
      name = "sonatype"
      url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
  }

  dependencies {
    // otel BOM
    implementation(platform("io.opentelemetry:opentelemetry-bom:${versions.opentelemetrySdk}"))

    // these serve as a test of the instrumentation BOMs
    implementation(platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:${versions.opentelemetryJavaagent}"))
    implementation(platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:${versions.opentelemetryJavaagentAlpha}"))

    testImplementation("org.mockito:mockito-core:${versions.mockito}")
    testImplementation("org.mockito:mockito-junit-jupiter:${versions.mockito}")
    testImplementation("org.awaitility:awaitility:4.2.0")
    testImplementation(enforcedPlatform("org.junit:junit-bom:${versions.junit}"))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
  }

  tasks {
    test {
      useJUnitPlatform()
    }

    compileJava {
      options.release.set(8)
    }
  }
}
