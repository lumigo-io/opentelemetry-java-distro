import com.google.protobuf.gradle.*

plugins {
  id("com.google.protobuf") version '0.8.18'
}

apply from: "$rootDir/gradle/java-conventions.gradle"

dependencies {
  implementation("io.grpc:grpc-core:${versions.grpc16}")
  implementation("io.grpc:grpc-protobuf:${versions.grpc16}")
  implementation("io.grpc:grpc-services:${versions.grpc16}")
  implementation("io.grpc:grpc-stub:${versions.grpc16}")
}

protobuf {
  protoc {
    // The artifact spec for the Protobuf Compiler
    artifact = "com.google.protobuf:protoc:3.3.0"
    if (osdetector.os == "osx") {
      // Always use x86_64 version as ARM binary is not available
      artifact += ":osx-x86_64"
    }
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:1.6.0"
      if (osdetector.os == "osx") {
        // Always use x86_64 version as ARM binary is not available
        artifact += ":osx-x86_64"
      }
    }
  }
  generateProtoTasks {
    all().configureEach {
      plugins {
        grpc { }
      }
    }
  }
}

tasks {
  compileJava {
    // We generate stubs using an old version of protobuf to test old versions of gRPC,
    // where this lint error triggers.
    options.compilerArgs.add("-Xlint:-cast")
  }
}
